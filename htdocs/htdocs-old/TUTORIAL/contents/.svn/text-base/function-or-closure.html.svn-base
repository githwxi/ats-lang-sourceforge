<HTML>
<BODY text="#000000" bgcolor="#ffffff" link="#0000FF" vlink="#CC00CC" alink="#ff0000">

<H2>Function or Closure?</H2>

<HR SIZE=1 ALIGN=LEFT><P>

It is possible in ATS to differentiate at the level of types functions
without environment from functions with environment.  This is an
indispensable feature for interfacing functions in C {\em directly} inside
ATS as there functions all have no environment. A function with an
environment is often referred to as a closure.

<H4>A problem with closures</H4>

In the programming language C, each function is at the toplevel, and it is
naturally represented as a pointer to the code heap where the code for the
function is stored. However, inner functions are supported in ATS, and they
may appear in the return value of a function call. As an example, the
following code in ATS involves an inner function <i>add_x</i> appearing in
the return value of an outer function <i>add</i>:

<pre>
fn add (x: int):&lt;cloref&gt; int -> int =
  let fn add_x (y: int):&lt;cloref&gt; int = x + y in add_x end
</pre>

The syntax <i>&lt;cloref&gt;</i> indicates that the defined functions
<i>add</i> and <i>add_x</i> are both (persistant) closure references.
Applying <i>add</i> to a given integer <i>i</i>, we obtain a unary function
that adds <i>i</i> to its argument. This function is represented as a pair
<i>(add_x_env, [x -> i])</i>, where we use <i>[x-&gt;i]</i> for something
often referred to as an environment that binds <i>x</i> to <i>i</i>, and
<i>add_x_env</i> for the (toplevel) function defined in the following
(pseudo) code:

<pre>
fun add_x_env (env, y) = env.x + y
</pre>

Note that the (pseudo) syntax <i>env.x</i> stands for the selection of the
value to which <i>x</i> is bound in the environment <i>env</i>.
<P>

We use the name <i>closure</i> to refer to a pair like <i>(add_x_env, [x ->
i])</i>. Given that functions may occur as arguments (of other functions)
in ATS, it is necessary that all functions be represented uniformly as
closures (if functions without environments cannot be differentiated from
functions with environments at compile-time). For instance, the
previously defined (toplevel) function <i>add</i> needs to be represented
as a pair <i>(add_env, [])</i>, where <i>[]</i> stands for the empty
environment and <i>add_env</i> is the function defined in the following
(pseudo) code:

<pre>
fun add_env (env, x) = (add_x_env, [x -> x])
</pre>

In functional languages like ML and Haskell, all functions are represented
as closures. Unfortunately, this requirement for representing all functions
as closures, can cause a serious difficulty when we try to use in ATS a
higher-order function implemented in C. Let us see a concrete example.
<P>

The function <i>qsort</i> is declared in &lt;stdlib.h&gt; with the following
type:

<pre>
void qsort(void *base, size_t nmemb, size_t size, int(*compar)(const void *, const void *));
</pre>

Clearly, <i>qsort</i> demands that its fourth argument be a function (not a
closure). In order to use <i>qsort</i> in ATS directly, we need a way to
construct functions represented as code pointers (instead of closures).
<P>

<h4>Types for functions with environment</h4>

In ATS, a type of the form <i>(T_1, ..., T_n) -&lt;cloref&gt; T_0</i> is
for a closure reference, that is, a reference to a function paired with an
environment that takes <i>n</i> arguments of types <i>T_1, ..., T_n</i> and
returns a value of type <i>T_0</i>.  The following
declaration states that <i>foo</i> is a closure reference of the type
<i>(T_1, ..., T_n) -&lt;cloref&gt; T_0</i>:

<pre>
val foo : (T_1, ...., T_n) -&lt;cloref&gt; T_0
</pre>

<h4>Types for functions without environment</h4>

In ATS, a type of the form <i>(T_1, ..., T_n) -&lt;fun&gt; T_0</i>,
where <i>fun</i> can be omitted, is for a
function without environment that takes <i>n</i> arguments of types <i>T_1,
..., T_n</i> and returns a value of type <i>T_0</i>. The
following two equivalent declarations both state that <i>bar</i> is a
function of the type <i>(T_1, ..., T_n) -&lt;fun&gt; T_0</i>:

<pre>
val bar : (T_1, ...., T_n) -&lt;fun&gt; T_0
fun bar (x_1: T_1, ...., x_n: T_n): T_0
</pre>

The previously mentioned function <i>qsort</i> can be given the following
type in ATS:

<pre>
fun qsort {a:viewt@ype} {n:nat}
  (base: ! @[a][n], nmemb: int n, size: sizeof_t a, compar: (!a, !a) -&lt;fun&gt; int)
  : void
</pre>

This type indicates that <i>qsort</i> itself is a function without
environment and its fourth argument is also a function without environment.
As an example, the following code implements a simple test on <i>qsort</i>:

<pre>
fn test_qsort () =

// creating a linear array of size 10
val (pf_A | p_A, sz_A) =
  @[int][1, 9, 2, 8, 3, 7, 4, 6, 5, 0]

// [cloref1] indicates that [pr] is a closure with full effects
fn pr (i: Nat, x: !int):&lt;cloref1&gt void = (if i > 0 print ", "; print x)

val () = (print "before quicksort:\n")
val () = iforeach_array_ptr_tsz {int} (pr, !p_A, sz_A, sizeof<int>)

val () = begin
  qsort {int} (!p_A, sz_A, sizeof<int>, lam (x, y) => compare (x, y))
end

val () = (print "after quicksort:\n")
val () = iforeach_array_ptr_tsz {int} (pr, !p_A, sz_A, sizeof<int>)

in
  array_ptr_free {int} (pf_A | p_A)
end
</pre>

<H4>Implementing a function without enviroment</H4>

The following code implements <i>add</i> as a function without enviroment:

<pre>
fn add0 (x: int):&lt;fun&gt; int -&lt;cloref&gt; int =
  let fn add0_x (y: int):&lt;cloref&gt; int = x + y in add0_x end
</pre>

The syntax <i>:&lt;fun&gt</i> is an annotation indicating that the type
ascribed to <i>add0</i> is <i>int -&lt;fun&gt; (int -&gt; int)</i>.  If we
change <i>int -&lt;cloref&gt; int</i> into
<i>int -&lt;fun&gt; int</i>, then an error is reported at compile-time as
the function <i>add0_x</i> does require a nonempty environment that binds
the variable <i>x</i> to some value. If <i>add0</i> is needed in a place
where a closure is expected, we can simply write
<i>lam x => add (x)</i> instead.
<P>
When a function is declared via the keyword <i>fun</i> or <i>fn</i>, it is
assumed by default that the function is without environment. For instance,
the following code is equivalent to the previous implementation of
<i>add0</i>:

<pre>
fn add0 (x: int): int -&lt;cloref&gt; int = begin
  let fn add0_x (y: int):&lt;cloref&gt; int = x + y in add0_x end
end
</pre>

On the other hand, the following code implements <i>add</i>
as a closure:

<pre>
fn add (x: int):&lt;cloref&gt; int -&lt;cloref&gt; int = begin
  let fn add_x (y: int):&lt;cloref&gt; int = x + y in add0_x end
end
</pre>

<H4>Linear Closures</H4>

As a closure is an aggregated value, memory allocation is required in order
to form closures. In ATS, linear closures are supported. As memory
allocated for linear closures can be freed explicitly by the programmer,
such closures are particularly useful in a situation where automatic
garbage collection is not allowed or its use needs to be significantly
reduced. We are to present detailed explanation on linear closures elsewhere.
<P>

<HR SIZE=1 ALIGN=LEFT><P>

The code used for illustration is available <a
href="function-or-closure.dats">here</a>.

</BODY>
</HTML>
